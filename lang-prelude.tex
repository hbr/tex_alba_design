\section{Prelude}

The compiler makes sure that the following definitions are always
available. It is as if there were a module \code{alba.core.prelude} and each
module would start with
\begin{alba}
  use
     alba.core.prelude
     ...
\end{alba}


Convention: Propositions (i.e. types) or functions returning a proposition are
written in lowercase. Other types are written in uppercase.





\subsection{Predicates and Relations}
\label{sec:predicates-and-relations}

\begin{alba}
  Predicate: Any -> Any
    := (\ A := A -> Proposition)

  Relation: Any -> Any -> Any
    := (\ A B := A -> B -> Proposition)

  Endorelation: Any -> Any
    := (\A := Relation A A)
\end{alba}
\vskip 2mm







\subsection{Propositions}
\label{sec:prelude-propositions}


\subsubsection{Implication}
%
\begin{alba}
  (=>) (a b:Proposition): Proposition
    := a -> b
\end{alba}

Propositions are types but unlike normal types we express propositions by
lowercase identifiers. The variables $a$ and $b$ are propositions, a term $p$
of type $a$ i.e. $p:a$ is a proof of the proposition.

The term $a\to b$ is just a shorthand for $\Pi x^a.b$ or
\begin{alba}
  all (_:a): b
\end{alba}
in program text. Therefore the term $0 < i \Rightarrow 0 \le i$ is a function
application with the value $\Pi p^{0 < i}. 0 \le i : \Prop$ or in program text
\begin{alba}
  0 < i => 0 <= i           : Proposition

  -- evaluates to
  (all (_:0 < i): 0 <= i)  : Proposition
\end{alba}
\vskip 2mm





\subsubsection{False}

The proposition \code{false} is a type which does not have any direct
proof. Therefore it is encoded as an inductive type with no constructor.
%
\begin{alba}
  class
    false: Proposition
  create
    -- no constructors
\end{alba}

The only possibility to prove \code{false} is to make some contradictory
assumptions. \vskip 2mm




\subsubsection{Negation}
%
\begin{alba}
  (not) (a:Proposition): Proposition
    := a => false
\end{alba}
\vskip 2mm






\subsubsection{Disjunction}
%
\begin{alba}
  class
    (or) (a b:Proposition): Proposition
  create
    left:  a => a or b
    right: b => a or b
\end{alba}

There are two ways to prove the expression \code{a or b} is to provide a proof
of the proposition $a$ and map it with the first constructor into a proof of
$a \lor b$ or provide a proof of $b$ and map it with the second constructor
into a proof of $a \lor b$.

The term \code{a => a or b} is a function application which evaluates to
\code{all (p:a) a or b} which is according to the typing rules a proposition
as well.
\newline




\subsubsection{Conjunction}
%
\begin{alba}
  class
    (and) (a b:Proposition): Proposition
  create
    conjunction: a => b => a and b
\end{alba}

In order to prove the proposition $a \land b$ we need a proof of $a$ and a
proof of $b$. The type of the constructor is a function type which maps a
proof of $a$ and a proof of $b$ to a proof of $a \land b$.
\newline





\subsubsection{Existential Quantification}

\begin{alba}
  class
    exist (A:Any) (f: Predicate A): Proposition
  create
    witness: all (a:A): f a -> exist f
\end{alba}

\begin{itemize}
\item The type variable $A$ is implicit, because the type of the second
  argument depends on it. Therefore the compiler can infer it from the second
  argument.

\item $f:A\to \Prop$ means that $f$ is a predicate. It maps any element of
  type $A$ into a proposition (not into a proof of the proposition).

\item In order to construct a valid term of this inductive type we have to
  provide a witness $a$ and a proof showing that $f a$ (which is a
  proposition) is satisfied.
\end{itemize}

Example
\begin{alba}
  exist (\(i:Natural) := 0 < i)
\end{alba}
\vskip 2mm


\subsubsection{Equality}
\label{sec:equality}

\begin{alba}
  class
    (=) (A:Any) (x:A): Predicate A
  create
    reflexive: x = x    -- long form: (=) A x x
\end{alba}

Note that \code{Predicate A} expands to \code{A -> Propositions}, therefore
\code{(=)} has three arguments. The type argument is implicit, therefore a
binary operator is ok.
\vskip 2mm








\subsection{Simple Types}
\label{sec:simple-types}



\subsubsection{List}

\begin{alba}
  class
    List (A:Any): Any
  create
    []: List A
    (^): A -> List A -> List A
\end{alba}
\vskip 2mm




\subsubsection{Maybe}

\begin{alba}
  class
    Maybe (A:Any): Any
  create
    just:    A -> Maybe A
    nothing: Maybe A
\end{alba}
\vskip 2mm


\subsubsection{Tuple}

\begin{alba}
  class
    (,) (A B:Any): Any
  create
    (,):    A -> B -> (A,B)
\end{alba}
\vskip 2mm




\subsubsection{Natural Numbers}

\begin{alba}
  class
    Natural: Any
  create
    0: Natural
    successor: Natural -> Natural
\end{alba}

The type of natural numbers can be used like an inductive type in the source
language. However the compiler implements the type with segmented arrays of
machine number with arbitrary precision arithmetic. Therefore natural numbers
are not as efficient as machine numbers but much more efficient as a
straightforward implemenation as an inductive type.


\begin{alba}
  class
    (<=): Endorelation Natural
  create
    start: all i: 0 <= i
    next:  all i j: i <= j => i.successor <= j.successor

  (<) (i j: Natural): Proposition := i.successor <= j
\end{alba}
\vskip 2mm


\subsubsection{Strings}

\begin{alba}
  class
    String: Any
  create
    "": String
    (^): Character -> String -> String
\end{alba}

The type of strings is specified like a list of characters. However the
compiler knows of this type and implements it as a segmented array of
characters which is much more space efficient.
\newline




\subsection{Strong Types}
\label{sec:prelude-strong-types}

Strong types are types with a computational and a propositional content. At
runtime all propositional content is erased. The propositional content serves
the compiler to carry information of proofs of certain properties which it can
use to proof more properties.


\subsubsection{Decision}
%
\begin{alba}
  class
    Decision (a b:Proposition): Any
  create
    left:  a -> Decision a b
    right: b -> Decision a b
\end{alba}

A term representing a decision between two propostions $a$ and $b$ needs
either a proof of $a$ or a proof of $b$. At runtime the proofs are erased and
only the information about the used constructor remains. Therefore a decision
value is like an enriched boolean value where the enrichment is removed at
runtime.

Note that the inductive types \code{or} for logical disjunction and
\code{Decision} are nearly identical. The only difference is that \code{or} is
a propositional type whose elements (aka proofs) are erased at runtime and
\code{Decision} is a normal type whose objects exist at runtime.
\newline




\subsubsection{Specified type}


Sometimes we have a number which we know to be positive or a prime number
etc. In order to specify this property in a type we need a predicate
describing this property. E.g.

\begin{alba}
  is_prime:       Natural -> Proposition
  (\ i := 0 < i): Natural -> Proposition
\end{alba}

The prelude of Alba contains an inductive type to describe types with a
certain property.

\begin{alba}
  class
    Specified (A:Any) (f:A -> Proposition): Any
  create
    specified: all (a:A): f a -> Specified f
    -- or shorter
    specified a: f a -> Specified f
\end{alba}

In the intermediate language this inductive definition is written as
$$
\Inductive_{[A:\Any, f:A \to \Prop], I:\Any, [\Pi a^A . f a \to I]}.
$$

\noindent Examples:
\begin{alba}
  Specified is_prime       -- Type of a prime natural number
  Specified (\i := 0 < i)  -- Type of a positive natural number
\end{alba}

In order to specify the type we have to provide the predicate describing the
property as the only argument. The base type is implicit. In order to make an
object of this type we have to provide the element of the base type and a
proof of the property (the proof will be generated from the context by the
compiler).

Note the similarity of the specified type and the existentially quantified
proposition. With an object of type \code{Specified is\_prime} it is possible
to pattern match and extract the object having the property and use it in
computations. With an object of type \code{exist is\_prime} it is possible to
pattern match and extract the object having the property but use it only for
other proofs and not for computations.

Convention: A specified type is used only as a result type of functions, never
as an argument. In the arguments of a function the object having the property
and the proof of the property are provided as two separate arguments. At the
call site only the object has to be given as an explicit argument, the proof
is generated by the compiler from the context.

We use the two projection functions to extract the value and the proof from a
specified value.

\begin{alba}
  value (A:Any) (f: Predicate A) (a: Specified f): A
    := inspect
         a
       case
         specified v := v

  proof (A:Any) (f: Predicate A) (a: Specified f): f (value a)
    := inspect
         a
       case
         specified _ := _
\end{alba}

Remark: The constructor \code{specified} constructs an object of
nonpropositional type. Therefore the proof argument is implicit. However the
compiler sees the proof and can build the term on the right hand side.

\vskip 2mm





\subsubsection{Strong Maybe}

The type \code{Strong\_maybe} is similar to the specified type. But it does
contain an object satisfying a specification only optionally. In case that
such an object satisfying the property does not exist, it contains a proof of
this fact.

Such a type is a typical output from a parsing function. Either the parser
returns a parse tree representing the input string according to the grammar or
a proof that there is no parse tree i.e. the input string does not conform to
the grammar.

\begin{alba}
   class
     Strong_maybe (A:Any) (f: Predicate A): Any
   create
     just a:  f a -> Strong_maybe f
     nothing: (all a: not (f a)) -> Strong_maybe f
\end{alba}




\subsection{Wellfounded Relation}

\begin{alba}
  class
    is_accessible (A:Any) (x:A) (r: Endorelation A): Proposition
  create
    access: (all y: r y x => y.is_accessible r) => x.is_accessible r

  is_wellfounded (A:Any) (r: Endorelation A): Proposition
    := all x: x.is_accessible r
\end{alba}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main_alba_design"
%%% End:
